USE PoMoMin
GO

CREATE PROCEDURE ASIGNAR_INVENTARIO
	@INVENTARIOID BIGINT,
	@EMPLEADOID BIGINT,
	@MOTIVOA INT,
	-- @FECHAVIGENTE DEFAULT GETDATE() DESDE CREACION DE TABLA
	-- @FECHAFIN NULL
	@NOTAS NTEXT,
	-- DATOS PARA BITACORA
	@USUARIOBITA NVARCHAR(100),
	@DISPOSITIVO NVARCHAR(200)
AS
DECLARE @MENSAJE AS VARCHAR(100);
DECLARE @NUEVASUCURSAL AS INT;
BEGIN TRY
BEGIN TRANSACTION
	IF @INVENTARIOID IS NULL OR @EMPLEADOID IS NULL OR @MOTIVOA IS NULL
	   BEGIN
			RAISERROR('Algunos parámetros no pueden ser nulos', 16, 1)
		END
	IF (SELECT AsignadoA FROM Inventario WHERE InventarioID = @INVENTARIOID) IS NOT NULL
	   BEGIN
			RAISERROR('El inventario ya está asignado a otro empleado', 16, 1)
		END

	-- INSERSION DE CAMPOS EN HISTORICO
	INSERT INTO InventarioEmpleados(InventarioID, EmpleadoID, MotivoAsignacionID, Notas)
	VALUES(@INVENTARIOID, @EMPLEADOID, @MOTIVOA, @NOTAS);

	-- UPDATE DE ASIGNADO A Y SUCURSAL DEL INVENTARIO SEGÚN EL EMPLEADO ASIGNADO
	SELECT @NUEVASUCURSAL = SucursalID FROM Empleados WHERE EmpleadoID = @EMPLEADOID;

	UPDATE Inventario
	SET AsignadoA = @EMPLEADOID,
		SucursalID = @NUEVASUCURSAL
	WHERE InventarioID = @INVENTARIOID

	-- BITACORA
	SET @MENSAJE = 'Asignación de inventario con ID: ' + CAST(@INVENTARIOID AS VARCHAR) + ' a empleado con ID: ' + CAST(@EMPLEADOID AS VARCHAR);

	EXEC INSERT_BITACORA @USUARIOBITA, 'ASIGNACION', 'COMPLETADO EXITOSAMENTE', @MENSAJE, @DISPOSITIVO;

	PRINT 'Registro insertado correctamente'
COMMIT TRANSACTION
END TRY
BEGIN CATCH
	ROLLBACK TRANSACTION
	SET @MENSAJE = 'Mensaje de error:' + ERROR_MESSAGE();
	EXEC INSERT_BITACORA @USUARIOBITA, 'ASIGNACION', 'NO COMPLETADO', @MENSAJE, @DISPOSITIVO;
END CATCH
GO