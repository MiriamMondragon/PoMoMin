USE PoMoMin
GO

CREATE PROCEDURE DESASIGNAR_INVENTARIO
	@INVENTARIOID BIGINT,
	@EMPLEADOID BIGINT,
	@MOTIVODES INT,
	-- @FECHAVIGENTE DEFAULT GETDATE() DESDE CREACION DE TABLA
	-- @FECHAFIN GETDATE() AL MOMENTO DE LA INSERCION DENTRO DEL SP
	@NOTAS NTEXT,
	-- DATOS PARA BITACORA
	@USUARIOBITA NVARCHAR(100),
	@DISPOSITIVO NVARCHAR(200)
AS
DECLARE @MENSAJE AS VARCHAR(100);
DECLARE @NUEVASUCURSAL AS INT;
BEGIN TRY
BEGIN TRANSACTION

	IF @INVENTARIOID IS NULL OR @EMPLEADOID IS NULL OR @MOTIVODES IS NULL
	   BEGIN
			RAISERROR('Algunos parámetros no pueden ser nulos', 16, 1)
		END

	-- ACTUALIZAR CAMPOS EN HISTORICO MODIFICANDO LA FECHA DE FIN DEL REGISTRO y MOTIVODESASIGNACION

	UPDATE InventarioEmpleados
	SET FechaFin = GETDATE(),
		MotivoDesasignacionID = @MOTIVODES
	WHERE InventarioID = @INVENTARIOID AND EmpleadoID = @EMPLEADOID AND FechaFin IS NULL

	-- QUITAR EL ASIGNADO A Y MARCAR EL EQUIPO COMO NORMAL, INACTIVO O EN ESPERA DE SOPORTE SEGUN MOTIVO

	IF @MOTIVODES = 3 OR @MOTIVODES = 9-- SOLO SE REALIZO UNA ACTUALIZACION DE EQUIPO O DESACTIVACION DE EMPLEADO POR LO QUE SE ENCUENTRA EN BUEN ESTADO
	BEGIN
		UPDATE Inventario
		SET AsignadoA = NULL
		WHERE InventarioID = @INVENTARIOID
	END

	IF @MOTIVODES = 4 -- SE CAMBIA POR SOPORTE TECNICO, POR LO QUE EL INVENTARIO SE MARCA COMO EN ESPERA DE SOPORTE
	BEGIN
		UPDATE Inventario
		SET AsignadoA = NULL,
			EstadoID = 3
		WHERE InventarioID = @INVENTARIOID
	END

	IF @MOTIVODES = 8 -- SE CAMBIA POR PERDIDA O DESTRUCCION, NO SE VOLVERA A USAR EL INVENTARIO POR LO QUE SE MARCA COMO INACTIVO
	BEGIN
		UPDATE Inventario
		SET AsignadoA = NULL,
			EstadoID = 2
		WHERE InventarioID = @INVENTARIOID
	END

	-- BITACORA
	SET @MENSAJE = 'Se desasignó el inventario con ID: ' + CAST(@INVENTARIOID AS VARCHAR) + ' del empleado con ID: ' 
				   + CAST(@EMPLEADOID AS VARCHAR);

	EXEC INSERT_BITACORA @USUARIOBITA, 'DESASIGNACION', 'COMPLETADO EXITOSAMENTE', @MENSAJE, @DISPOSITIVO;

	PRINT 'Registro actualizado correctamente'
COMMIT TRANSACTION
END TRY
BEGIN CATCH
	ROLLBACK TRANSACTION
	SET @MENSAJE = 'Mensaje de error:' + ERROR_MESSAGE();
	EXEC INSERT_BITACORA @USUARIOBITA, 'DESASIGNACION', 'NO COMPLETADO', @MENSAJE, @DISPOSITIVO;
END CATCH
GO